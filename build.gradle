plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE' apply false
    id 'maven-publish'
}

group 'de.adesso'
version '0.0.1-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        dependencies {
            dependency "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
            dependency "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
            dependency "org.assertj:assertj-core:${assertJVersion}"
            dependency("fr.inria.gforge.spoon:spoon-core:${spoonCoreVersion}") {
                exclude 'fr.inria.gforge.spoon:spoon-core'
            }
            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "org.apache.commons:commons-math3:${commonsMathVersion}"
            dependency "org.apache.commons:commons-lang3:${commonsLangVersion}"
            dependency "commons-io:commons-io:${commonsIoVersion}"
            dependency "org.slf4j:slf4j-log4j12:${slf4jVersion}"
            dependency "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
        }
    }

    dependencies {
        // Log4j
        implementation 'org.slf4j:slf4j-log4j12'

        // Spoon
        // use a locally built version with fixes for https://github.com/INRIA/spoon/issues/3365
        // see README.md for more details on how to build the jar file
        implementation files("${project.rootDir}/libs/spoon-core-8.1.1-SNAPSHOT.jar")
        implementation 'fr.inria.gforge.spoon:spoon-core'

        // Apache Commons
        implementation 'org.apache.commons:commons-math3'
        implementation 'org.apache.commons:commons-lang3'
        implementation 'commons-io:commons-io'

        // JUnit + Mockito + AssertJ
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.mockito:mockito-junit-jupiter'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testImplementation 'org.assertj:assertj-core'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    compileJava {
        options.compilerArgs << '-Xlint:unchecked'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                // use jar target for publish
                artifact jar

                groupId = "${rootProject.group}"
                artifactId = "${rootProject.name}-${project.name}"
                version = "${rootProject.version}"
            }
        }
    }

}

